.model tiny
.data
PORTA1	EQU	00H	;INITIALISING ADDRESSES FOR 8255(1)
PORTB1	EQU	02H
PORTC1	EQU	04H
CTR1	EQU	06H	;CONTROL WORD ADDRESS

PORTA2	EQU	10H	;INITIALISING ADDRESSES FOR 8255(2)
PORTB2	EQU	12H
PORTC2	EQU	14H
CTR2	EQU	16H	;CONTROL WORD ADDRESS

UNAVA	DB	?	;UNAVAILABILITY  BYTE OBTAINED FROM PB0-PB7 OF 8255(2)
QUASEL	DB	?	;QUANTITY SELECTED BYTE OBTAINED FROM PA0-PA7 OF 8255(1)
SODASEL	DB	?	;SODA SELECTED BYTE OBTAINED FROM PA0-PA7 OF 8255(1)

AROTATE1	DB	01H,02H,04H,08H,09H	;ANTICLOCKWISE ROTATION SEQUENCE FOR MOTOR SODA1,3
CROTATE1	DB	08H,04H,02H,01H,09H	;CLOCKWISE ROTATION SEQUENCE FOR MOTOR SODA1,3
AROTATE2	DB	10H,20H,40H,80H,90H	;ANTICLOCKWISE ROTATION SEQUENCE FOR MOTOR SODA2
CROTATE2	DB	80H,40H,20H,10H,90H	;CLOCKWISE ROTATION SEQUENCE FOR MOTOR SODA2

.stack
.code
	
.startup

BEGIN:	MOV	AL,10010000B		;CONTROL WORD INITIALISATION FOR 8255(1) AND 8255(2)
	OUT	CTR1,AL
	MOV	AL,10001010B
	OUT	CTR2,AL
	MOV	AL,0
	OUT	PORTB1,AL
	OUT	PORTC1,AL


	IN	AL,PORTB2		;CHECKING SODA UNAVAILABILITY
	MOV	BL,AL
	AND	BL,00000111B
	ROR	BL,1
	JC	X1			
	MOV	AL,00H			;X1,X2,X3 ARE LOCATED AT END OF CODE
X1A:	ROR	BL,1
	JC	X2
X2A:	ROR	BL,1
	JC	X3
X3A:	OUT	PORTC1,AL
	MOV	UNAVA,AL


XS:	IN	AL,PORTA1		;SODA SELECTION 
	NOT	AL
	AND	AL,00000111B
	CMP	AL,0
	JA	SELECT
	JMP	XS
SELECT:	OUT	PORTB1,AL
	MOV	SODASEL,AL

	AND	AL,UNAVA		;CHECKING CONFLICT WITH UNAVAILABILITY
	CMP	AL,0
	JNZ	XT

XQ:	IN	AL,PORTA1		;QUANTITY SELECTION
	NOT	AL
	AND	AL,00111000B
	CMP	AL,0
	JA	X4
	JMP	XQ
X4:	MOV	QUASEL,AL
	OR	AL,SODASEL
	OUT	PORTB1,AL


	MOV	AL,QUASEL		;COIN ACCEPTANCE
	CMP	AL,00001000B
	JZ	X5
	CMP	AL,00010000B		;X5,X6,X7 LOCATED AT END OF CODE
	JZ	X6
	CMP	AL,00100000B
	JZ	X7
COIN:	CALL	DELAY			;TIME DELAY SO THAT SINGLE PUSH OF BUTTON IS NOT COUNTED AS MANY
	IN	AL,PORTC2
	AND	AL,80H
	CMP	AL,80H
	JNZ	COIN
	DEC	CL
	CMP	CL,0
	JNZ	COIN

	
DIS:	IN	AL,PORTA1		;GLOWING DISPENSED LED AFTER PRESSING DISPENSE BUTTON
	NOT 	AL
	AND	AL,01000000B
	CMP	AL,40H
	JNZ	DIS
	MOV	AL,08H
	OR	AL,UNAVA
	OUT	PORTC1,AL		


	MOV	AL,SODASEL		;ROTATING THE REQUIRED MOTOR CLOCKWISE AFTER 
	CLD				;CHECKING WHICH SODA WAS SELECTED USING SODASEL
	CMP	AL,00000001B
	JNZ	Y1	
	LEA	SI,CROTATE1
F1:	LODSB	
	OUT	PORTA2,AL
	CALL	DELAYS
	CMP	AL,09H
	JNZ	F1
	JMP	Q1

Y1:	CMP	AL,00000010B
	JNZ	Y2
	LEA	SI,CROTATE2
F2:	LODSB	
	OUT	PORTA2,AL
	CALL	DELAYS
	CMP	AL,90H
	JNZ	F2
	JMP	Q1

Y2:	LEA	SI,CROTATE1
F3:	LODSB	
	OUT	PORTC2,AL
	CALL	DELAYS
	CMP	AL,09H
	JNZ	F3



Q1:	MOV	AL,QUASEL		;SETTING TIME DELAY TO DISPENSE QUANTITY
	CMP	AL,00001000B		;AFTER CHECKING QUANTITY SELECTED USING QUASEL
	JNZ	Q2
	MOV	CX,6
	CALL	DELAY1
	JMP	Z1
Q2:	CMP	AL,00010000B
	JNZ	Q3
	MOV	CX,12
	CALL	DELAY1
	JMP	Z1
Q3:	MOV	CX,18
	CALL 	DELAY1



Z1:	MOV	AL,SODASEL		;CLOSING VALVE BY ROTATING MOTOR ANTI CLOCKWISE.
	CLD				;SAME LOGIC AS CLOCKWISE ROTATION ABOVE USING SODASEL
	CMP	AL,00000001B
	JNZ	Y1B	
	LEA	SI,AROTATE1
F1B:	LODSB	
	OUT	PORTA2,AL
	CALL	DELAYS
	CMP	AL,09H
	JNZ	F1B
	JMP	XT

Y1B:	CMP	AL,00000010B
	JNZ	Y2B
	LEA	SI,AROTATE2
F2B:	LODSB	
	OUT	PORTA2,AL
	CALL	DELAYS
	CMP	AL,90H
	JNZ	F2B
	JMP	XT

Y2B:	LEA	SI,AROTATE1
F3B:	LODSB	
	OUT	PORTC2,AL
	CALL 	DELAYS
	CMP	AL,09H
	JNZ	F3B


XT:	MOV	CX,2			;END OF PROGRAM
	CALL	DELAY1
	JMP	BEGIN

X1:	MOV	AL,01H
	JMP	X1A
X2:	OR	AL,00000010B
	JMP	X2A
X3:	OR	AL,00000100B
	JMP	X3A


X5:	MOV	CL,1
	JMP	COIN
X6:	MOV	CL,2
	JMP	COIN
X7:	MOV	CL,3
	JMP	COIN

.EXIT
DELAY	PROC	NEAR			;DELAY FOR COIN ACCEPTOR SO THAT SINGLE PUSH IS NOT COUNTED AS MANY
	MOV	BX,3000H
XD:	NOP
	DEC	BX	
	JNZ	XD
	RET
DELAY	ENDP

DELAY1	PROC	NEAR			;STEP DELAY OF 0.5 SECONDS WITH 1.5MHz
AB:	MOV	BX,8B82H		;USED TO SET DELAY FOR QUANTITY DISPENSING 3,6,9 SECONDS
AD:	NOP	
	DEC	BX
	JNZ	AD
	DEC	CX
	JNZ	AB
	RET
DELAY1	ENDP

DELAYS	PROC	NEAR			;DELAY FOR MOTOR SIGNALS
	MOV	BX,1800H		;SO MOTOR ROTATES PROPERLY
AF:	NOP	
	DEC	BX
	JNZ	AF
	RET
DELAYS	ENDP
END